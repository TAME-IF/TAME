action general a_quit named "quit";
action general a_look named "look around", "look", "l";
action general a_take named "take", "pick up", "t", "p";
action transitive a_examine named "look at", "examine", "x";
action ditransitive a_use named "use" uses conjunctions "with", "and";

object o_key named "key", "small key"
{
	onAction(a_examine)
	{
		textln("Looks like a small key.");
	}
	
	onAction(a_take)
	{
		if (!hasObject(player, o_key))
		{
			giveObject(player, o_key);
			textln("You take the key.");
		}
		else
			textln("You already have this.");
	}
	
	onRoomBrowse()
	{
		textln("There's a key, here.");
	}
	
	onPlayerBrowse()
	{
		textln("A key.");
	}
	
}

object o_door named "door" uses determiners "the"
{
	init()
	{
		locked = true;
	}

	onAction(a_examine)
	{
		textln("Looks like a wooden door.");
		addObjectName(this, "wooden door");
	}
	
	onActionWith(a_use, o_key)
	{
		locked = false;
		textln("You unlocked the door!");
	}
	
	onAction(a_take)
	{
		textln("You can't pick up a door.");
	}
	
	onAction(a_use)
	{
		if (locked)
			textln("The door is locked.");
		else
		{
			textln("You open the door. You escaped the room!");
			quit;
		}
	}
	
}

player p_guy
{
}

room r_startroom
{
	init()
	{
		
	}
}

world
{
	start()
	{
		giveObject(r_startroom, o_key);
		giveObject(r_startroom, o_door);
		setPlayer(p_guy);
		setRoom(player, r_startroom);
	}
}
